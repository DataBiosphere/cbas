dependencies {
    // Version controlled by dependency management plugin
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.glassfish.jersey.core:jersey-client'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart'

    implementation 'io.swagger.core.v3:swagger-annotations'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli'
}

def artifactGroup = "${group}.dockstore"

tasks.register('downloadDockstoreYaml', Download) {
    // this was the latest git commit hash at development time
    src "https://raw.githubusercontent.com/dockstore/dockstore/bff1a2e0459f2d95faf0c8acfa575cf41e46e342/dockstore-webservice/src/main/resources/openapi3/openapi.yaml"
    dest "${buildDir}/dockstore-openapi.yaml"
}

generateSwaggerCode {
    inputFile = file("${buildDir}/dockstore-openapi.yaml")
    language = 'java'
    library = 'jersey2'

    // For Swagger Codegen v3 on Java 16+
    // See https://github.com/swagger-api/swagger-codegen/issues/10966
    jvmArgs = ['--add-opens=java.base/java.util=ALL-UNNAMED']

    components = [
        apiDocs  : false, apiTests: false,
        modelDocs: false, modelTests: false
    ]

    additionalProperties = [
        modelPackage  : "${artifactGroup}.model",
        apiPackage    : "${artifactGroup}.api",
        invokerPackage: "${artifactGroup}.client",
        dateLibrary   : 'java11',
        java8         : true
    ]

    rawOptions = ['--ignore-file-override', "${projectDir}/.swagger-codegen-ignore"]
}

generateSwaggerCode.dependsOn downloadDockstoreYaml

idea.module.generatedSourceDirs = [file("${generateSwaggerCode.outputDir}/src/main/java")]
sourceSets.main.java.srcDir "${generateSwaggerCode.outputDir}/src/main/java"
compileJava.dependsOn generateSwaggerCode
